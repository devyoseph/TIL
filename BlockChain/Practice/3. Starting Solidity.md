# 3. Starting Solidity

## 1) 솔리디티 컨트랙트 기본 구조

```solidity
// SPDX-License-Identifier: GPL-3.0: 소스코드의 라이선스 명

/* Version Pragma: 소스코드가 이용하는 컴파일러 버전 명시
 - Sematic versioning을 따름
 - ^ (캐럿 연산자): '이상'
*/
pragma solidity >=0.7.0 <0.9.0;

/*
 - 블록체인(contract storage)에 값이 저장되는 변수
 - 상태 변수의 접근 제어자(Visibility) 지정 가능 => external, public, private
 - 기본형, 구조체, 배열 등 다양한 자료형 존

*/
contract Storage {

    uint256 number;

    /**
     * @dev Store value in variable
     * @param num value to store
     */
    function store(uint256 num) public {
        number = num;
    }

    /**
     * @dev Return value 
     * @return value of 'number'
     */
    function retrieve() public view returns (uint256){
        return number;
    }
}
```

<br>

#### [1] 상태 변수 State Variable

- 블록체인(contract storage)에 값이 저장되는 변수
- 상태 변수의 접근 제어자(Visibility) 지정 가능 => external, public, private
- 기본형, 구조체, 배열 등 다양한 자료형 존재

<br>

#### [2] 함수  Function

* 컨트랙트 단위 기능

* 매개 변수, 제어자, 반환값 지정 가능

* 함수 내부에서 상태 변수의 값을 변경(write)하고 읽을(read) 수 있음

<br>

## 2) 예제로 기본 문법 익히기

<br>

### [1] 자료형, 접근 제어자, 자주 쓰는 자료형

1. 기본형 Primitives
   
   ```solidity
   // SPDX-License-Identifier: GPL-3.0
   pragma solidity >=0.7.0 <0.9.0;
   
   contract Primitives {
   
       // Unassigned variables have a default value
       bool public defaultBool; // false
       uint public defaultUint; // 0
       int public defaultInt; // 0
       address public defaultAddr; // 0x0000000000000000000000000000000000000000
       string public defaultString; // ''
   
       // addr value assignment
       address public addr = 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4;
   
       /*
       non negative integers
       different sizes are available
           uint8   ranges from 0 to 2 ** 8 - 1
           uint16  ranges from 0 to 2 ** 16 - 1
           ...
           uint256 ranges from 0 to 2 ** 256 - 1
       
       uint is same as uint256
       */
       uint8 public u8 = 1;
       uint public u = 123; 
       uint256 public u256 = 456;
   
       uint public maxUint = type(uint).max;
       uint public maxUint256 = type(uint256).max;
   
       /*
       integer
       different sizes are available, like uint
   
       int256 ranges from -2 ** 255 to 2 ** 255 - 1
       int128 ranges from -2 ** 127 to 2 ** 127 - 1
       */
       int8 public i8 = -1;
       int public i = -123; 
       int256 public i256 = 456;
   
       // minimum and maximum of int
       int public minInt = type(int).min;
       int public maxInt = type(int).max;
   
       /*
       In Solidity, the data type byte represent a sequence of bytes. 
       Two bytes types in Solidity:
   
        - fixed-sized byte arrays: bytes#
        - dynamically-sized byte arrays. byte[]
       */
       bytes1 a = 0xb5; //  [10110101]
       bytes1 b = 0x56; //  [01010110]  
   }
   
   ```
   
   
   
   * `1_Primitives.sol`을 Remix에 추가하여 컴파일, 배포
     
     * 논리형
       
       * bool: true or false
     
     * 정수형
       
       * uint: unsigned integer
       
       * int: signed integer
         
         * 8 ~ 256 bit를 표현할 수 있으며, uint는 uint256과 같음
     
     * 주소형
       
       * address: 이더리움의 주소를 표현
     
     * 바이트형
       
       * bytes# or byte[]: 데이터를 바이트로 표현할 수 있음

2. 접근 제어자 Visibility
   
   ```solidity
   // SPDX-License-Identifier: GPL-3.0
   pragma solidity >=0.7.0 <0.9.0;
   
   contract Parent {
       // State variables
       string private privateVar = "private variable";
       string internal internalVar = "internal variable";
       string public publicVar = "public variable";
       
       // Private function 
       function privateFunc() private pure returns (string memory) {
           return "private function called";
       }
   
       function testPrivateFunc() public pure returns (string memory) {
           return privateFunc();
       }
   
       // Internal function 
       function internalFunc() internal pure returns (string memory) {
           return "internal function called in Parent Contract";
       }
   
       function testInternalFunc() public pure virtual returns (string memory) {
           return internalFunc();
       }
   
       // Public functions
       function publicFunc() public pure returns (string memory) {
           return "public function called";
       }
   
       // External functions 
       function externalFunc() external pure returns (string memory) {
           return "external function called";
       }
   }
   
   contract Child is Parent {
       // Internal function call be called inside child contracts.
       function testInternalFunc() public pure override returns (string memory) {
           return internalFunc();
       }
   }
   
   ```
   
   * `2_Visibility.sol`을 Remix에 추가하여 컴파일, Parent, Child 컨트랙트 각각 배포
   
   * Remix 상에 나타나는 접근 가능한 버튼과 호출 시 반환 값을 확인
   
   ![](assets/2022-08-26-20-07-53-image.png)

3. 배열 Array
   
   ```solidity
   // SPDX-License-Identifier: GPL-3.0
   pragma solidity >=0.7.0 <0.9.0;
   
   contract Array {
       // Dynamic sized array
       uint[] public arr; // Not initialized
       uint[] public arr2 = [1, 2, 3]; // Initialized
       // Fixed sized array, all elements initialize to 0
       uint[10] public fixedSizeArr;
   
       // Compare with accessing state variable
       function get(uint i) public view returns (uint) {
           return arr2[i];
       }
   
       // Append new element to array
       // Check array size after calling this function.
       function push(uint i) public {
           arr.push(i);
       }
   
       // Remove last element from array
       // Check array size after calling this function.
       function pop() public {
           arr.pop();
       }
   
       // Reset the value at index
       // Check array size after calling this function.
       function remove(uint index) public {
           delete arr[index];
       }
   
       // returns the length of array.
       function getLength() public view returns (uint) {
           return arr.length;
       }
   
       // returns the entire array.
       function getArr() public view returns (uint[] memory) {
           return arr;
       }
   
       function createArray() external pure returns (uint[] memory){
           // create array in memory, only fixed size can be created
           uint[] memory a = new uint[](5);
           return a;
       }
   }
   ```
   
   * `3_Array.sol` 만들어 배포
   
   * 고정 길이와 유동적 길이를 갖는 배열을 선언하는 법
   
   * 배열형 자료구조를 다루는 방법
     
     * index 접근하기
     
     * push pop delete
   
   * 함수 내에서 로컬 변수로 배열을 사용하기 위해서는 고정 길이로 선언해야 함

4. 매핑(Mapping)
   
   ```solidity
   // SPDX-License-Identifier: UNLICENSED
   pragma solidity >=0.7.0 <0.9.0;
   
   contract Mapping {
       // Mapping from address to uint
       mapping(address => uint) public addrToUint;
   
       // Access value with key in Mapping.
       // If there is no key, it will return the default value, 0.
       function get(address _addr) public view returns (uint) {
           return addrToUint[_addr];
       }
   
       // Update the value at the address
       function set(address _addr, uint _i) public {
           addrToUint[_addr] = _i;
       }
   
       // Reset the value to the default value.
       function reset(address _addr) public {
           delete addrToUint[_addr];
       }
   }
   ```
   
   * `4_Mapping.sol`을 Remix에 추가
   
   * 매핑형 선언 방법
     
     * `mapping(address => uint) public addrToUint;`
   
   * 접근, 추가, 삭제 방법
   
   * 매핑에 저장된 **key의 목록을 얻을 수 있는 방법을 제공하지 않음**.

5. 사용자 선언 자료형(Struct)
   
   * 여러 자료형을 하나의 관점으로 묶어서 관리하고자 할 때 선언
     
     ```solidity
     struct Todo {
         string text;
         bool completed;
     }
     ```
   
   * `5_Struct.sol`을 Remix에 추가하여 컴파일, 배포
   
   * 구조체의 Array, Mapping의 값으로 지정 가능
   
   * 구조체 생성, 접근, 변경 방법
   
   * 함수 안에서 struct 상태 변수를 참조하는 방법

6. 함수형(Function)
   
   ```solidity
   // SPDX-License-Identifier: UNLICENSED
   pragma solidity >=0.7.0 <0.9.0;
   
   contract Function {
   
       uint public num = 1;
   
       uint public a = 1;
       string public s = "hello solidity";
       bool public b = true;
   
       // No parameter and return value
       function addOne() public {
           num++;
       }
   
       // One parameter and a return value
       function addNumber(uint x) public returns (uint) {
           num += x;
   
           return num;
       }
    
       // view - not to modify the state variable, but read.
       function addAndReturn(uint x) public view returns (uint) {
          return num + x;
       }
   
       // pure - not to modify or read the state variable.
       function add(uint x, uint y) public pure returns (uint) {
          return x + y;
       }
   
       // Return many values
       function returnMany() public view returns (uint, string memory, bool) {
           return (a, s, b);
       }
   
   }
   ```
   
   * `view`: gas가 없이 순순하게 값만 가져온다.
     
     * 경제적 프로그래밍
   
   * `pure`: 굳이 변수들을 로드하지 않아도 사용할 수 있는 메서드
   
   * 두 개 이상 반환하는 법도 있음
     
     * `return (a, s, b);`

7. 조건문 If-Else
   
   ```solidity
   // SPDX-License-Identifier: UNLICENSED
   pragma solidity >=0.7.0 <0.9.0;
   
   contract IfElse {
       function foo(uint x) public pure returns (uint) {
           if (x < 10) {
               return 0;
           } else if (x < 20) {
               return 1;
           } else {
               return 2;
           }
       }
   
       function ternary(uint _x) public pure returns (uint) {
           return _x < 10 ? 1 : 2;
       }
   }
   
   contract Loop {
       function loop1() public pure {
           for (uint i = 0; i < 10; i++) {
               if (i == 3) {
                   continue;
               }
               if (i == 5) {
                   break;
               }
           }
       }
   
       function loop2() public pure {
           uint i;
           while (i < 10) {
               i++;
           }
       }
   }
   ```
   
   * string 완전머신
     
     * if문을 제공
     
     * gas의 개념: 반복문 돌리는 것을 막음(=gas limit)

8. ehter, wei, gwei
   
   * 소수점이 불가능하기 때문에 단위를 잘 알고 있어야 한다.
   
   ```solidity
   // SPDX-License-Identifier: UNLICENSED
   pragma solidity >=0.7.0 <0.9.0;
   
   contract EtherUnits {
       uint public oneWei = 1 wei;
       uint public oneGwei = 1 gwei;
       uint public oneEther = 1 ether;
     
       // 1 wei is equal to 1
       bool public isOneWei = 1 wei == 1;
   
       // 1 ether is equal to 10^18 wei
       bool public isOneEther1 = oneEther == 1e18;
   
       // 1 ether is equals to 10^9 gwei.
       bool public isOneEther2 = oneEther == 10**9 * oneGwei;
   
       // 1 gwei is equals to 10^9 wei.
       bool public isOneGwei = oneGwei == 10**9 * oneWei;
   }
   ```
   
   * `8_EtherUnits.sol`
     
     

<br>

### [2] 사용자 선언형, 함수, 제어문, 화폐 단위


